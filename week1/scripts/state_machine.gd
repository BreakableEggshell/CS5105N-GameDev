class_name StateMachine
extends Node

@export var initial_state: State

var current_state: State
var states: Dictionary = {}

func _ready() -> void:
	var player := get_parent() as CharacterBody2D
	await player.ready
	for child in get_children():
		if child is State:
			states[child.name.to_lower()] = child
			child.actor = player
			child.sprite = player.get_node("AnimatedSprite2D")
			child.transitioned.connect(_on_transitioned)
	if initial_state == null and not states.is_empty():
		initial_state = states.values()[0]
	if initial_state:
		current_state = initial_state
		current_state.enter()
	else:
		push_warning("StateMachine has no states")

func _unhandled_input(event: InputEvent) -> void:
	if current_state:
		current_state.handle_input(event)

func _process(delta: float) -> void:
	if current_state:
		current_state.update(delta)

func _physics_process(delta: float) -> void:
	if current_state:
		current_state.physics_update(delta)

func _on_transitioned(new_state_name: StringName) -> void:
	var new_state: State = states.get(new_state_name.to_lower())
	if new_state == null or new_state == current_state:
		return
	current_state.exit()
	current_state = new_state
	current_state.enter()
